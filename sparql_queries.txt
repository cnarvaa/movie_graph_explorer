# movie general data
SELECT ?movie ?movieLabel ?main_subjectLabel ?genreLabel ?box WHERE {
    ?movie wdt:P345 "tt0107290";
    wdt:P921 ?main_subject;
    wdt:P136 ?genre.
    ?movie p:P2142 ?statement.
    ?statement ps:P2142 ?box
    SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
}

# country age
SELECT
    ?countryLabel ?population ?area ?medianIncome ?age
WHERE {
    ?country wdt:P463 wd:Q458.
    OPTIONAL { ?country wdt:P1082 ?population }
    OPTIONAL { ?country wdt:P2046 ?area }
    OPTIONAL { ?country wdt:P3529 ?medianIncome }
    OPTIONAL { ?country wdt:P571 ?inception.
    BIND(year(now()) - year(?inception) AS ?age)
    }
    SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
}

#most box_office movies
SELECT DISTINCT ?movie ?movieLabel ?box WHERE {
    SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
    ?movie wdt:P31 wd:Q11424;
    p:P2142 ?statement;
    p:P495 ?country_statement.
    ?statement ps:P2142 ?box.
    ?country_statement ps:P495 wd:Q30.
    FILTER(?box >= 623933331 )
}
ORDER BY DESC (?box)
LIMIT 100

#production_company for most successful movies
SELECT DISTINCT ?movie ?movieLabel ?box ?prod_companyLabel WHERE {
    SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
    OPTIONAL {
    ?movie p:P272 ?prod_company_statement.
    ?prod_company_statement ps:P272 ?prod_company.
    }
    {
    SELECT * WHERE {
        ?movie wdt:P31 wd:Q11424;
        p:P2142 ?statement;
        p:P495 ?country_statement.
        ?statement ps:P2142 ?box.
        ?country_statement ps:P495 wd:Q30.
        FILTER(?box >= 623933331 )
    }
    LIMIT 100
    }
}
ORDER BY DESC (?box)
LIMIT 10
# other movies from the most successful production production company
SELECT DISTINCT ?movieLabel ?box ?prod_companyLabel ?extra_movieLabel WHERE {
    SERVICE wikibase:label { bd:serviceParam wikibase:language "en". }
    ?extra_movie p:P272 ?prod_company_statement.
    ?prod_company_statement ps:P272 ?prod_company.
    {
    SELECT ?movie ?prod_company ?box WHERE {
        OPTIONAL {
        ?movie p:P272 ?prod_company_statement.
        ?prod_company_statement ps:P272 ?prod_company.
        }
        {
        SELECT * WHERE {
            ?movie wdt:P31 wd:Q11424;
            p:P2142 ?statement;
            p:P495 ?country_statement.
            ?statement ps:P2142 ?box.
            ?country_statement ps:P495 wd:Q30.
            FILTER(?box >= 623933331 )
        }
        LIMIT 100
        }
    }
    ORDER BY DESC (?box)
    LIMIT 2
    }
}
ORDER BY DESC (?prod_companyLabel)